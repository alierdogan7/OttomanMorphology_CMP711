define Alphabet [ ا | ب | پ | ت | ث | ج | چ | ح | خ | د | ذ | ر | ز | ژ | س | ش | ص | ض | ط | ظ | ع | غ | ف | ق | ك | گ | ڭ | ل | م | ن | و | ه | ی | ي ];
define Vowels [ ا | ه | ى | ي | و ];
define NonVowels [ ب | پ | ت | ث | ج | چ | ح | خ | د | ذ | ر | ز | ژ | س | ش | ص | ض | ط | ظ | ع | غ | ف | ق | ك | گ | ڭ | ل | م | ن ];

! (I)m, (I)n, (I)mIz, (I)nIz, (s)I insertion
define PossesiveKaynastirmaInsertion [..] -> ي || NonVowels  "^" _ [ م ز | م | ڭ ز ] ,,
                                     [..] -> س || Vowels  "^" _ [ ى ];

! Case Insertions
define NInsertion   [..] -> [ن] || "^" [ ى | ل ر ى ] "^" _ [ د ن | د ه | ه | ى ] ,, ! After (s)I and larI -DA -DAn become -nDA -nDAn, (y)A becomes nA (y)I becomes nI
                    [..] -> ن || Vowels "^" _ ڭ ; ! (n)In

define YInsertion   [..] -> ي || Vowels "^" _ [ ى | ه | ل ه  ] ; !(y)I, (y)A, (y)lA

! Cleanup: remove morpheme boundaries
define Cleanup "^" -> 0;

read lexc ottoman_noun.lexc
define Lexicon

define Grammar Lexicon           .o.
               PossesiveKaynastirmaInsertion       .o.
               NInsertion .o.
               YInsertion .o.
               Cleanup        ;

regex Grammar;